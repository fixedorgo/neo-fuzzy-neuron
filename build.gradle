apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.7
targetCompatibility = 1.7
compileJava.options.encoding = 'UTF-8'

group = "com.fixedorgo"
archivesBaseName = "neo-fuzzy-neuron"
version = "0.1.0-SNAPSHOT"
description = "A simple library that describes neo-fuzzy-neuron as part of hybrid neural network based system modeling."

repositories {
    mavenCentral()
}

dependencies {
    compile (
        'com.google.guava:guava:18.0'
    )
    testCompile (
        'junit:junit:4.11',
        'org.assertj:assertj-core:1.7.0'
    )
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Neo-Fuzzy-Neuron'
                packaging 'jar'
                description 'A simple library that describes neo-fuzzy-neuron as part of hybrid neural network based system modeling.'
                url 'https://github.com/fixedorgo/neo-fuzzy-neuron'

                scm {
                    connection 'scm:git:git@github.com:fixedorgo/neo-fuzzy-neuron.git'
                    developerConnection 'scm:git:git@github.com:fixedorgo/neo-fuzzy-neuron.git'
                    url 'git@github.com:fixedorgo/neo-fuzzy-neuron.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'fixedorgo'
                        name 'Timur Zagorskiy'
                        email 'timur.zagorskiy@gmail.com'
                    }
                }
            }
        }
    }
}
